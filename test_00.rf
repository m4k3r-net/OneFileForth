.( ::: Test file for One File Forth ::: ) cr

1 constant on
0 constant off
32 constant space
1024 dup * constant 1Meg
freespace cellsize / constant start_mem
utime constant start_time
" test.log" save constant log_file

: set_trace trace ! ;
: set_log log_file outfile ;
: clr_log closeout ;
: tab 9 emit ;
: done 13 emit 8 0 do tab loop ." [DONE]" cr ; 

.( ::: Loop test 1 begin-while-repeat ::: )
: operations begin dup while -- repeat drop ;
done
noops 1Meg dup tab . ." loop operations " utime swap operations utime swap -
ops . ." forth operations in " . ." microseconds" cr

.( ::: code decompiled ::: )
set_log
' operations see 
clr_log
done

.( ::: Loop test 2 begin-again ::: )
: fast 1Meg begin -- dup 0 == if drop leave then again ; 
done
noops 1Meg tab . ." loop operations in " utime fast utime ops . ." forth operations in " swap - . ." microseconds " cr

.( ::: code decompiled ::: ) 
set_log
' fast see
clr_log
done

.( ::: tracing 10 operations [indefinite loop] ::: )
set_log
on set_trace
10 operations
off set_trace
clr_log
done

.( ::: testing definite loop ::: )
: tstdo 0 do loop ;
done
noops 1Meg dup tab . ." do loop operations " utime swap tstdo utime swap -
ops . ." forth operations in " . ." microseconds" cr

.( ::: code decompiled ::: ) 
set_log
' tstdo see 
clr_log
done

.( ::: tracing 10 do loop iterations ::: )
set_log
on set_trace
10 tstdo
off set_trace
clr_log
done

.( ::: arithmetic tests ::: )
set_log
." addition: 3 == " 1 2 + . cr
." subraction: 0 == " 2 2 - . cr
." multiplication: 81 == " 9 9 * . cr
." division: 4 == " 12 3 / . cr
clr_log
done

.( ::: ) 1Meg . .( 1 byte tty i/o operations ) cr
: sspin 0 do spin loop ;
utime 1Meg sspin utime swap - ops tab . ." forth operations in " . .( microseconds ) done cr

.( ::: stack tests ::: ) 
set_log
1 2 3 4 5
.( ::: stack depth is ) depth . cr
.( ::: stack view is ) .s .( ::: )  cr 
.( ::: clear stack by printing ::: ) cr
. . . . . cr
clr_log
done

.( ::: loop backwards by 2 ::: )
: bkw 0 100 do i . -2 +loop ;
set_log
bkw cr
clr_log
done

.( ::: loop forwards by 2 ::: )
: fwd 100 0 do i . 2 +loop ;
set_log
fwd cr
clr_log
done

.( ::: conditional tests [modulo 5] ::: )
: conditional 0 do i 5 % if 46 emit else i . then loop ; 
set_log
100 conditional cr
clr_log
done

.( ::: found in comp.lang.forth thread ::: )
: test begin ?dup if -- else leave then again ;
utime 100000000 test utime swap - . ." u-secs" cr

.( ::: word list ::: ) 
set_log words clr_log done cr

.( ::: end of tests ::: ) cr
.( ::: output can be viewed in file ) log_file type space emit ."  ::: " cr
.( ::: error: ) errval dup . errstr type cr
.( ::: total memory usage: ) 
freespace cellsize / start_mem swap - . ." bytes." cr
.( ::: total elapsed time: ) 
utime start_time - 1000000 / . ." seconds." cr

forget words bye
